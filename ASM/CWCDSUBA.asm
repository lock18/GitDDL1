*ASM CICS (NOEPILOG,NOPROLOG)
**********************************************************************
*   THE FOLLOWING IS AN ASSEMBLER SUBROUTINE WITHOUT ANY COMMAND     *
*   LEVEL CODE.  DO NOT RUN THIS THRU THE COMMAND LEVEL TRANSLATOR.  *
*   IT IS CALLED BY A COBOL 'CALL' IN THE COBOL DEMO PROGRAMS AND    *
*   BY A PL/I 'CALL' IN THE PL/I PGM CWDEMPL.                        *
*                                                                    *
*   THIS SUBROUTINE RECEIVES THE EMPLOYEE NUMBER AND NUMBER OF HOURS *
*   WORKED FROM THE CALLER.  IT THEN SEARCHES A TABLE FOR THE        *
*   EMPLOYEE NUMBER TO FIND THE EMPLOYEES HOURLY RATE.  TOTAL PAY IS *
*   FOUND BY MULTIPLYING HOURS * RATE.  THE HOURLY RATE AND TOTAL    *
*   PAY IS PASSED BACK TO THE CALLER.                                *
**********************************************************************
CWCDSUBA CSECT
CWCDSUBA AMODE ANY
CWCDSUBA RMODE ANY
*   STANDARD HOUSEKEEPING AND LINKAGE CONVENTIONS...
         STM   R14,R12,12(R13)       SAVE CALLING PGM'S REGISTERS.
         LR    R12,R15               ESTABLISH ADDRESSABILITY.
         USING CWCDSUBA,R12          REG 12 IS THE BASE REG.
         L     R4,4(,R1)             A(WORK AREA)
         USING CWCDWRK,R4
         XC    0(WRKLEN,R4),0(R4)    CLEAR WORKAREA
         ST    R13,SAVEAREA+4        SAVE CALLERS SAVEAREA ADDRESS
         LA    R15,SAVEAREA          GET ADDRESS OF THIS PGMS SAVEAREA
         ST    R15,8(R13)            SAVE IT HERE
         LR    R13,R15               R13 = THIS PGMS SAVEAREA
*   START PAYROLL PROCESSING.....
         L     R2,0(,R1)             LOAD ADDR OF CALLING PGM LIST.
         MVC   LISTAREA(20),0(R2)    MOVE CALLING PGM LIST TO LISTAREA.
         MVC   LSTRATE,=C'00000'     ZERO OUT LISTAREA RATE FIELD.
         MVC   LSTTPAY,=C'0000000'   ZERO OUT LISTAREA TOTAL PAY FLD.
         ZAP   PAKTPAY,=P'+0'        ZERO OUT TOTAL PAY PACKED FIELD.
         LA    R6,EMPTBL             LOAD ADDR OF EMPLOYEE TABLE.
TBLLOOP  CLC   LSTNUM,0(R6)          SEARCH FOR EMP NUM IN TABLE.
         BE    CALCPAY               FOUND IT, CALCULATE PAY.
         LA    R6,8(,R6)             GET NEXT EMP NUM IN TABLE.
         C     R6,=A(EMPTBL+32)      ARE WE AT THE LAST EMP IN TABLE?
         BNE   TBLLOOP               NO, REPEAT TABLE LOOP.
CALCPAY  MVC   LSTRATE(3),5(R6)      MOVE RATE TO LISTAREA.
         PACK  PAKTPAY(5),LSTRATE    MOVE RATE TO A PACKED FIELD.
         PACK  PAKHOURS(2),LSTHOURS  MOVE HOURS TO A PACKED FIELD.
         MP    PAKTPAY,PAKHOURS      MULTIPLY RATE * HOURS.
         UNPK  LSTTPAY,PAKTPAY       MOVE TOTAL PAY TO LISTAREA.
         OI    LSTTPAY+6,X'F0'       STRIP OFF SIGNED BYTE.
         MVC   0(20,R2),LISTAREA     PASS DATA BACK TO CALLING PGM.
         L     R13,SAVEAREA+4        LOAD ADDRESS OF PREVIOUS STACK.
         LM    R14,R12,12(R13)       RESTORE REGISTERS.
         BR    R14                   GO BACK TO CALLING PROGRAM.
         LTORG
*
*
EMPTBL   DS    0F            EMPLOYEE NUMBER/RATE TABLE
         DC    CL5'00010'    EMPLOYEE NUMBER 10
         DC    CL3'010'      EMPLOYEE NUMBER 10 RATE:  $10/HOUR
         DC    CL5'00020'    EMPLOYEE NUMBER 20
         DC    CL3'020'      EMPLOYEE NUMBER 20 RATE:  $20/HOUR
         DC    CL5'00030'    EMPLOYEE NUMBER 30
         DC    CL3'030'      EMPLOYEE NUMBER 30 RATE:  $30/HOUR
         DC    CL5'00040'    EMPLOYEE NUMBER 40
         DC    CL3'040'      EMPLOYEE NUMBER 40 RATE:  $40/HOUR
         DC    CL5'00050'    EMPLOYEE NUMBER 50
         DC    CL3'050'      EMPLOYEE NUMBER 50 RATE:  $50/HOUR
*
CWCDWRK  DSECT
DATA     DS    0C
STARTPS  DC    CL24'CWCDSUBA PROGRAM STORAGE'
*
SAVEAREA DS    18F
*
PAKTPAY  DS    PL5
PAKHOURS DS    PL2
LISTAREA DS    0F
LSTNUM   DS    CL5
LSTHOURS DS    CL3
LSTRATE  DS    CL5
LSTTPAY  DS    CL7
WRKLEN   EQU   (*-DATA)
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   CWCDSUBA